Algoritmo DecomposicaoPrimos;

{ 

    inteiro N, NOriginal, fator, potencia;  
    logico primeiroFator; 

    escreva ("Digite um número inteiro maior que 1 para decompor:");
    leia(N);

    NOriginal = N;
    fator = 2;
    primeiroFator = verdadeiro;
    escreva (NOriginal, " = ");

    ENQUANTO (N > 1)
    {
        potencia = 0; 

        ENQUANTO (N % fator == 0)
        {
            potencia = potencia + 1; 
            N = N / fator;        
        }

        SE (potencia > 0) ENTÃO
        {
            SE (primeiroFator == falso) ENTÃO
            {
                escreva (" * ");
            }
            escreva (fator);

            // Se a potência for maior que 1, exibe o expoente
            SE (potencia > 1) ENTÃO
            {
                escreva ("^", potencia); // Exemplo: 2^3
            }

            // Marca que o primeiro fator já foi exibido
            primeiroFator = falso;
        }

        // Passa para o próximo fator
        fator = fator + 1;
    }
    escreva ("\n"); 
}